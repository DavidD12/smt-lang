use std::str::FromStr;
use crate::problem::*;
use super::*;
use line_col::LineColLookup;
use fraction::Fraction;
// use std::time::Duration;

grammar<'a>(lookup: &LineColLookup<'input>, parser: &mut Parser<'a>);

match {
    r"\s*" => { }, // The default whitespace skipping is disabled an `ignore pattern` is specified
    r"//[^\n\r]*[\n\r]*" => { }, // Skip `// comments`
    r"/\*[^*]*\*+(?:[^/*][^*]*\*+)*/" => { },  // Skip `/* comments */`
    _
}

//------------------------- Problem -------------------------

pub Problem: () = Entry* => {

};

//------------------------- Entry -------------------------

Entry: () = {
    Include,
    Variable,
    Constraint,
};

//------------------------- Include -------------------------

Include: () = "include" <file:String> => {
    parser.add(file);
};

//------------------------- Variable -------------------------

Variable: () = "let" <i:Identifier> ":" <t:Type> <e:Assign?> => {
    let position = Some(i.position.clone());
    let v = Variable::new(i.name, t, e, position);
    parser.problem.add_variable(v);
};    

//------------------------- Constraint -------------------------

Constraint: () = "constraint" <i:Identifier> <e:Assign> => {
    let position = Some(i.position.clone());
    let c = Constraint::new(i.name, e, position);
    parser.problem.add_constraint(c);
};    

//------------------------- Assign -------------------------

Assign: Expr = "=" <e:Expr> => e;

//========================= Type =========================

Type: Type = {
    "Bool" => Type::Bool,
    "Int"  => Type::Int,
    "Real" => Type::Real,
    <min:Integer> ".." <max:Integer> => Type::Interval(min as isize, max as isize),
};

//========================= Expr =========================

Expr: Expr = ImpliesOrExpr;

ImpliesOrExpr: Expr = {
    <left:ImpliesOrExpr> <l:@L><op:ImpliesOrOp> <right:AndExpr> => {
        let position = Some(Position::new(parser.file(), lookup, l));
        Expr::BinExpr(Box::new(left), op, Box::new(right), position)
    },
    AndExpr,
};

AndExpr: Expr = {
    <left:AndExpr> <l:@L>"and" <right:CompExpr> => {
        let position = Some(Position::new(parser.file(), lookup, l));
        Expr::BinExpr(Box::new(left), BinOp::And, Box::new(right), position)
    },
    CompExpr,
};

CompExpr: Expr = {
    <left:CompExpr> <l:@L><op:CompOp> <right:AddSubExpr> => {
        let position = Some(Position::new(parser.file(), lookup, l));
        Expr::BinExpr(Box::new(left), op, Box::new(right), position)
    },
    AddSubExpr,
};

AddSubExpr: Expr = {
    <left:AddSubExpr> <l:@L><op:AddSubOp> <right:MulDivExpr> => {
        let position = Some(Position::new(parser.file(), lookup, l));
        Expr::BinExpr(Box::new(left), op, Box::new(right), position)
    },
    MulDivExpr,
};

MulDivExpr: Expr = {
    <left:MulDivExpr> <l:@L><op:MulDivOp> <right:PreExpr> => {
        let position = Some(Position::new(parser.file(), lookup, l));
        Expr::BinExpr(Box::new(left), op, Box::new(right), position)
    },
    PreExpr,
};

PreExpr: Expr = {
    <l:@L><op:PreOp> <e: PreExpr> => {
        let position = Some(Position::new(parser.file(), lookup, l));
        Expr::PreExpr(op, Box::new(e), position)
    },
    Term,
};


Term: Expr = {
    <l:@L> <b:Boolean> => {
        let position = Position::new(parser.file(), lookup, l);
        Expr::BoolValue(b, Some(position))
    },
    <l:@L> <i:Integer> => {
        let position = Position::new(parser.file(), lookup, l);
        Expr::IntValue(i as isize, Some(position))
    },
    <l:@L> <d:Decimal> => {
        let position = Position::new(parser.file(), lookup, l);
        Expr::RealValue(d, Some(position))
    },
    <x:Identifier> => Expr::Unresolved(x.name, Some(x.position)),
    "(" <Expr> ")",
};

//------------------------- Operator -------------------------

ImpliesOrOp: BinOp = {
    "=>" => BinOp::Implies,
    "or" => BinOp::Or,
};

CompOp: BinOp = {
    "=" => BinOp::Eq,
    "/=" => BinOp::Ne,
    "<" => BinOp::Lt,
    "<=" => BinOp::Le,
    ">=" => BinOp::Ge,
    ">" => BinOp::Gt,
};

AddSubOp: BinOp = {
    "+" => BinOp::Add,
    "-" => BinOp::Sub,
};

MulDivOp: BinOp = {
    "*" => BinOp::Mul,
    "/" => BinOp::Div,
};

PreOp: PreOp = {
    "not" => PreOp::Not,
    "-" => PreOp::Minus,
};

//========================= Util =========================

CommaOne<T>: Vec<T> = {
    <mut v:(<T> ",")*> <e:T> => {
        v.push(e);
        v
    }
};

//========================= Lexer =========================

String: String = <s:r#"".+""#> => s[1..s.len()-1].to_string();

// Duration: Duration = {
//     <i:UInteger> "sec" => Duration::from_secs(i),
//     <i:UInteger> "ms" => Duration::from_millis(i),
// };

Boolean: bool = {
    "false" => false,
    "true" => true,
};

Integer: i64 = <i:r"-?[0-9]+"> => i.parse::<i64>().unwrap();

// UInteger: u64 = <i:r"[0-9]+"> => i.parse::<u64>().unwrap();

Decimal: Fraction = <d:r"[0-9]+\.[0-9]+"> => Fraction::from_str(d).unwrap();

Identifier: Identifier = <l:@L> <s:r"[a-zA-Z_][a-zA-Z_0-9]*"> <r:@R> => {
    Identifier::new(parser.file(), lookup, s, l)
};